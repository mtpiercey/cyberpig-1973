      SUBROUTINE INITN(IN, VEN)
      
      COMMON /INIT/ NR, NW, ND, NT7, NT8, NDAT, NS1, NS2
      COMMON /RAN/ IRN(54), KRANUM, RLIMIT, NRNOPT, ITAPE
      COMMON /STAT/ SUM(5, 20), SUM2(5, 10), SUM3(5, 10), SUM4(5, 10)
      COMMON /GEN/ITOT, ISIZE, NOWT, NOWY, NDAYS, NRUNS, IATRIB(18), ATRIB(4)
      COMMON /HOUSE/ NH(6), N1, N2, N3, N4, N5, N6, NXTFAR, MX1, MX2, MX3, MX4, X1, X2, X4
      COMMON /SALES/ MDSELL, WTS, NXTSAL, NSLD(4), WTSLD(4), VSLD(4), PRIH, PRIS
      COMMON /BRED/ LBREED, NBREED, IFAR(6, 5), IDB(30), KAGEW, LGTHW, NAGEM
      COMMON /BRED2/ ABNORG, GESM, GESS, GMIN, GMAX, ESTM, ESTS, EMIN, EMAX
      COMMON /BRED3/ PTRM1, PTRM2, PTRM16, PCON1, PCON2, ADJ1, ADJ2
      COMMON /SKIP/ ISTOP4, ISTOP5, ISTOP6, ISTOP7, MAXSKP
      COMMON /SURVIV/ PW1, PW23, PSRT, PSFIN, PSOWS, PBORS, PBORS1, MDEAD(8), PSRT2
      COMMON /GAIN/ GW1, GW23, GSRT, GFIN1, GFIN2, GSOWM, GSOWL, SDP, SSOWM, SSOWL
      COMMON /BIRTH/ BRNM, BRNS, BMIN, BMAX, BWTM, BWTS, BWMIN, BWMAX, NOBRN
      COMMON /CLEAN/ NDPREV, NH4CLN, LOSSL, SWTLP, SSLP
      COMMON /CULL/ NGLTS, MXSRV, MXAGS, PCULG, PCULS, PSSR, PRGT, MXBOR, LDP
      COMMON /COST/ FCS, FCB, CLAB, TFAR, TNUR, TSOW, TFIN
      COMMON /BOAR/ MXSDY, MXSWY, MXSDM, MXSWM, NPUR, IPDAT, KAGE, WTPB, IBS
      COMMON /RPT1/ ISMYP, ISMB, ISMBG, ISMF, ISMW, ISMS, ISMM
      COMMON /RPT2/ ISMYC, ISMCS, ISMCH, ISML, ISMSS, ISMSH, ISMGR
      COMMON /RPT3/ MJEV, ISALS, IFARS, IBRDS
      COMMON /RPT4/ IDCS, IDCE, IDCN, IDCF, IDCM, IDCB, IDCFS, IDCFP, IOFLW
      COMMON /RPT5/ IAAS, IAAE, IAAN, IAAFN, IAAM, IAAB, IAAFR
      COMMON /SALE2/ DUMMY(12), DIS
      COMMON /KRS/ KRS1, KRS2, KRS3, KRS4, KRS5, KRS6, KRS7, KRPT

      DIMENSION RR(10)

      INTEGER DATE
      DIMENSION DATE(8)

      CHARACTER(10) :: DAY

      INTEGER XMINUS
      DATA XMINUS/000000000000000007777/

      OPEN(NEWUNIT=NR, FILE="NR.txt", STATUS="OLD", ACTION="READ", FORM="FORMATTED")
      OPEN(NEWUNIT=NDAT,FILE="NDAT.txt",STATUS="OLD",ACTION="READ",FORM="FORMATTED")


C ASSIGN TAPES
C   READ=NR=  WRITE=NW, DIAGNOSTIC=ND, BASE HERD DATA TAPE=NDAT,
C       RANDOM NOS. = ITAPE
C     NR = 2
C     NW = 6

C MTP: CHANGED FROM 6 SO IT ISN'T STDOUT
      NW = 60

      ND = 13

C MAY SET TO 8 FOR JUNKING VIA CARD 56.
      NT7 = 130
C     NDAT = 15
      ITAPE = 50
      MR = 0

C INITIATE VARIABLES OUTSIDE SUBROUTINES
      KRANUM = 0
      RLIMIT = 2.0 ** 48.0 - 1.0
      ITOT = 0
      DO 90 II = 1,6
90    NH(II) = 0
      NOWY = 0

C PRESET STATISTICAL COLLECTION ARRAYS
      DO 100 JVARBL = 1,20
100   CALL ZROSUM(SUM, JVARBL)
      DO 200 IVARBL = 1,10
      CALL ZROSUM(SUM2, IVARBL)
      CALL ZROSUM(SUM3, IVARBL)
200   CALL ZROSUM(SUM4, IVARBL)


C PRESET FARROWING DATES FOR FIRST YEAR
      IFAR(1, 1) = 15
      IFAR(2, 1) = 60
      IFAR(3, 1) = 152
      IFAR(4, 1) = 198
      IFAR(5, 1) = 244
      IFAR(6, 1) = 355


C MARK BEGINNING OF PRINTED OUTPUT
      WRITE(ND, 606) ND
      WRITE(NT7, 606) NT7
      WRITE(NW, 606) NW
606   FORMAT('8   BEGIN PRINT TAPE'I4' REPORTS')


C READ NR=TAPE5 DATA AND LIST THEM.
      READ(NR, 705) RR
705   FORMAT(10A8)
706   FORMAT(//' FREE FORM IDENT CARD FOLLOWED BY DEFAULT DATA, USER OPTION CARDS '/' APPEAR AFTER CORRESPONDING DEFAULT CARD'//5X,10A8)
      WRITE(ND, 706) RR


C READ STANDARD 10F7.0, FIRST CELL IS CARD NO., OTHERS
C REPLACE WITH NON-BLANK CELLS IF FIND USER OPTION CARD NO.
30    READ(NR, 50) RR
      WRITE(ND, 60) RR

      IR = RR(1)
      IF(IR .EQ. 8888) GO TO 999

50    FORMAT(10F7.0)
60    FORMAT(5X,10F9.3)

      IF(IR .GT. 20) GO TO 500
      GO TO(1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20) IR

C ISIZE OF OPENING INVENTORY HERDS
1     ISIZE = RR(2)
      GO TO 30

C FARROWING CONTROL
2     LBREED = RR(2)
      NBREED = RR(3)
      KAGEW = RR(4)
      LGTHW = RR(5)
      NAGEM = RR(6)
      GO TO 30

C SURVIVAL PROBABILITIES, CONVERT TO DAILY, PUT INTO /SURVIV/.
3     PW1 = (1.0 - RR(2)) ** (1.0 / 7.0)
      PW23 = (1.0 - RR(3)) ** (1.0 / 14.0)
      PSRT = (1.0 - RR(4)) ** (1.0 / 35.0)
      PSRT2 = (1.0 - RR(5)) ** (1.0 / 35.0)
      PSFIN = (1.0 - RR(6)) ** (1.0 / 124.0)
      PSOWS = (1.0 - RR(7)) ** (1.0 / 365.0)
      PBORS = (1.0 - RR(8)) ** (1.0 / 365.0)
      PBORS1 = (1.0 - RR(9)) ** (1.0 / 365.0)
      GO TO 30

C FILL /BRED2/ AND /BRED3/
C ABNORMAL GILTS, ESTRUS LENGTH, PROBABILITY OF CONCEPT, ADJ. NO BORN
4     ABNORG = RR(2)
      ESTM = RR(3)
      ESTS = RR(4)
      EMIN = RR(5)
      EMAX = RR(6)
      PCON1 = RR(7)
      PCON2 = RR(8)
      ADJ1 = RR(9)
      ADJ2 = RR(10)
      GO TO 30

C GESTATION LENGTH AND PREMATURE TERMINATION.
5     GESM = RR(2)
      GESS = RR(3)
      GMIN = RR(4)
      GMAX = RR(5)
      PTRM1 = 1.0 - ((1.0 - RR(6)) ** (1.0 / 7.0))
      PTRM2 = 1.0 - ((1.0 - RR(7)) ** (1.0 / 100.0))
      PTRM16 = 1.0 - ((1.0 - RR(8)) ** (1.0 / 7.0))
      GO TO 30

C FILL /BIRTH/
6     BRNM = RR(2)
      BRNS = RR(3)
      BMIN = RR(4)
      BMAX = RR(5)

C WEIGHTS FILLED INTO /BIRTH/
      BWTM = RR(6)
      BWTS = RR(7)
      BWMIN = RR(8)
      BWMAX = RR(9)
      GO TO 30

C WEIGHT INTO /GAIN/
7     GW1 = RR(2)
      GW23 = RR(3)
      GSRT = RR(4)
      GFIN1 = RR(5)
      GFIN2 = RR(6)
      SDP = RR(7) / 100.0
      GO TO 30

C SALES CONTROL INTO /SALES/
8     MDSELL = RR(2)
      WTS = RR(3)
      PRIH = RR(4)
      PRIS = RR(5)
      DIS = RR(6) / 100.0
      GO TO 30

C SOW GAINS
9     GSOWM = RR(2)
      SSOWM = RR(3)
      GSOWL = RR(4)
      SSOWL = RR(5)

C   ADD TO /CLEAN/
      SWTLP = RR(6)
      SSLP = RR(7)
      GO TO 30

C OPENING INVENTORY, BOARS - NEW, YOUNG, THEN MATURE.
10    NB1 = RR(2)
      IF(NB1 .LT. 1) GO TO 6200
      DO 6101 K = 1,NB1
      IATRIB(1) = RR(3)
      IATRIB(2) = 1
      IATRIB(3) = 1
      IATRIB(4) = 1
      IATRIB(5) = 88 * 1000 + K
      ATRIB(1) = RR(4)
      ATRIB(2) = RNORM(GSOWM, SSOWM, -5.0, 10.0)
      CALL FILEM(IN, VEN, 8)
6101  CONTINUE

6200  NB2 = RR(5)
      IF(NB2 .LT. 1) GO TO 6300
      DO 6201 K = 1,NB2
      IATRIB(1) = RR(6)
      IATRIB(2) = 1
      IATRIB(3) = 2
      IATRIB(4) = 1
      IATRIB(5) = 88 * 1000 + NB1 + K
      ATRIB(1) = RR(7)
      ATRIB(2) = RNORM(GSOWM, SSOWM, -5.0, 10.0)
      CALL FILEM(IN, VEN, 8)
6201  CONTINUE

6300  NB3 = RR(8)
      IF(NB3 .LT. 1) GO TO 30
      DO 6301 K = 1,NB3
      IATRIB(1) = RR(9)
      IATRIB(2) = 1
      IATRIB(3) = 3
      IATRIB(4) = 1
      IATRIB(5) = 88 * 1000 + NB2 + NB1 + K
      ATRIB(I) = RR(10)
      CALL FILEM(IN, VEN, 8)
6301  CONTINUE
      GO TO 30

C CULLING OF GILTS AND SOWS IN /CULL/
11    NGLTS = RR(2)
      MXSRV = RR(3)
      MXAGS = RR(4)
      PCULG = RR(5) / 100.0
      PCULS = RR(6) / 100.0
      MXBOR = RR(7)
      LDP = RR(8)
      GO TO 30

C CONCEPTION ADJUSTMENT, ADDED TO /CULL/
12    PSSR = RR(2)
      PRGT = RR(3)
      GO TO 30

C COST CARD
13    FCS = RR(2)
      FCB = RR(3)
      CLAB = RR(4)
      TFAR = RR(5)
      TNUR = RR(6)
      TSOW = RR(7)
      TFIN = RR(8)
      GO TO 30

C BUILDING OPTIONS, BUILDINGS ARE NOT IN FILES AS ARE ANIMALS.
14    MX1 = RR(2)
      NDPREV = RR(3)
      X1 = RR(4)
      GO TO 30

15    MX2 = RR(2)
      X2 = RR(3)
      GO TO 30

16    MX3 = RR(2)
      GO TO 30

17    MX4 = RR(2)
      X4 = RR(3)
      GO TO 30

C NO SUCH CARDS
18    GO TO 1350
19    GO TO 1350

C BOAR MANAGEMENT OPTIONS
20    MXSDY = RR(2)
      MXSWY = RR(3)
      MXSDM = RR(4)
      MXSWM = RR(5)
      NPUR = RR(6)
      IPDAT = RR(7)
      KAGE = RR(8)
      WTPB = RR(9)
      GO TO 30

C CARDS 51. AND GREATER
500   IF(IR-51) 1350, 51, 502
502   IF(IR-53) 52, 53, 504
504   IF(IR-55) 54, 55, 506
506   IF(IR-57) 56, 57, 508
508   IF(IR-59) 58, 1350, 1350

C SIMULATION CONTROL -NDAYS,NRUNS
51    NDAYS = RR(2)

C MTP: REPLACED THE CDC BUILT-IN RANF FUNCTION WITH RAND
C      AND CHANGED SEED = RR(3) NSEED SO IT IS AN INTEGER
      NSEED = RR(3)
      SEED = RAND(NSEED)
      MAXSKP = RR(4)
      NRNOPT = RR(5)
      KRS4 = RR(6)
      GO TO 30

C NO CARD 52.
52    GO TO 1350

C PRODUCTION SUMMARY REPORT CONTROL
53    ISMYP = RR(2)
      ISMB = RR(3)
      ISMBG = RR(4)
      ISMF = RR(5)
      ISMW = RR(6)
      ISMM = RR(7)
      ISMM = RR(8)
      GO TO 30

C COSTS AND RETURNS SUMMARY REPORT CONTROL
54    ISMYC = RR(2)
      ISMCS = RR(3)
      ISMCH = RR(4)
      ISML = RR(5)
      ISMSS = RR(6)
      ISMSH = RR(7)
      ISMGR = RR(8)
      GO TO 30

C EVENTS AS THEY OCCUR REPORT CONTROL
55    MJEV = RR(2)
      ISALS = RR(3)
      IFARS = RR(4)
      IBRDS = RR(5)
      GO TO 30

C DAILY INVENTORY REPORT CONTROL
56    IDCS = RR(2)
      IDCE = RR(3)
      IDCN = RR(4)
      IDCF = RR(5)
      IDCM = RR(6)
      IDCB = RR(7)
      IDCFS = RR(8)
      IDCFP = RR(9)
      IOFLW = RR(10)
      GO TO 30

C INDIVIDUAL DAILY ANIMAL ATTRIBUTES CONTROL
57    IAAS = RR(2)
      IAAE = RR(3)
      IAAN = RR(4)
      IAAFN = RR(5)
      IAAM = RR(6)
      IAAB = RR(7)
      IAAFR = RR(8)
      GO TO 30

C NS1,NS2 DIAGNOSTIC SWITCHES
58    NS1 = RR(2)
      NS2 = RR(3)
      GO TO 30
1350  WRITE(ND, 1351) RR(1)
1351  FORMAT(' ERROR SUB. INITN, DATA CARD .GT. PERMITTED='F8.2)
      GO TO 30

C MTP: RETROFITTED TODAY'S DATE FROM CDC'S BUILT-IN DATE FUNCTION
999   CALL DATE_AND_TIME(values=DATE)
      WRITE(DAY,'(I4.4, "-", I2.2, "-", I2.2)') INT(DATE(1)), INT(DATE(2)), INT(DATE(3))

      WRITE(NW, 998) NDAYS, DAY

998   FORMAT(//18X,'SWINE BREEDING HERD SIMULATOR'//7X,'REQUEST IS FOR',I4,' DAYS,',10X,'TODAYS DATE IS ',A10/7X///17X)

      CLOSE(NR)

      RETURN
      END